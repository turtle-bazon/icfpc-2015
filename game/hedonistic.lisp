
(in-package :hextris)

(defclass hedonistic-solver (solver)
  ())

(defparameter *sum-of-heights-factor* 2.75)
(defparameter *row-burn-factor* 1.0)
(defparameter *one-burnt-row-penalty* -5.0)
(defparameter *blockade-factor* -7.5)
(defparameter *touching-something-factor* 3.0)
(defparameter *touching-wall-factor* 3.5)
(defparameter *touching-floor-factor* 5.0)
(defparameter *row-fill-factor* 10)

;; 0: 7101, 10: 5538, 2: 5568
;; (defparameter *sum-of-heights-factor* 0.7172661)
;; (defparameter *row-burn-factor* -0.13707352)
;; (defparameter *blockade-factor* 2.702942)
;; (defparameter *touching-something-factor* 8.942196)
;; (defparameter *touching-wall-factor* 8.346035)
;; (defparameter *touching-floor-factor* 5.843904)
;; (defparameter *row-fill-factor* 4.588444)
;; (defparameter *distict-power-words-count-factor* 2.64328)
;; (defparameter *total-power-words-count-factor* 8.137441)
;; (defparameter *one-burnt-row-penalty* 8.351305)

(defun count-rows-fill (field)
  (declare (optimize (speed 3))
           (type hextris-map field))
  (iter (declare (declare-variables))
        (for (the fixnum row) from 0 below (the fixnum (height field)))
        (for (values row-count row-sum) =
             (iter (declare (declare-variables))
                   (with (the fixnum cont) = 0)
                   (with (the fixnum sum) = 0)
                   (with (the fixnum count) = 0)
                   (for (the fixnum col) from 0 below (the fixnum (width field)))
                   (for cv = (not (map-cell-free-p* field row col)))
                   (if cv
                       (progn
                         (incf count)
                         (incf cont)
                         (incf sum cont))
                       (setf cont 0))
                   (finally (return (values count sum)))))
        (for row-estimate = (/ (+ row-sum row)
                               (expt 2 (- (height field) row 1))))
        (summing row-estimate)))

(defun find-out-sum-of-heights (field)
  (declare (optimize (speed 3))
           (type hextris-map field))
  (iter (declare (declare-variables))
        (with (the fixnum heights) = 0)
        (for (the fixnum col) from 0 below (the fixnum (width field)))
        (iter (declare (declare-variables))
              (for (the fixnum row) from 0 below (the fixnum (height field)))
              (unless (map-cell-free-p* field row col)
                (incf heights (- (the fixnum (height field)) row))
                (terminate)))
        (finally (return heights))))

(defun find-out-burned-rows-count (field)
  (declare (optimize (speed 3))
           (type hextris-map field))
  (multiple-value-bind (map-with-burned-rows burned-rows-count)
      (map-burn-lines-v2 field)
    (declare (ignore map-with-burned-rows)
             (type fixnum burned-rows-count))
    (cond ((zerop burned-rows-count) 0.0)
          ((= burned-rows-count 1) *one-burnt-row-penalty*)
          (t (ash 2 burned-rows-count)))))

(defun find-out-blockades-count-col (field col)
  (declare (optimize (speed 3) (safety 0))
           (type hextris-map field))
  (iter (declare (declare-variables))
        (with (the fixnum blockades-count) = 0)
        (with blocked-p = nil)
        (for (the fixnum row) from 0 below (the fixnum (height field)))
        (if (map-cell-free-p* field row col)
            (when blocked-p
              (incf blockades-count))
            (setf blocked-p t))
        (finally (return blockades-count))))

(defun find-out-blockades-count (field)
  (declare (optimize (speed 3) (safety 0))
           (type hextris-map field))
  (iter (declare (declare-variables))
        (with (the fixnum total) = 0)
        (for (the fixnum col) from 0 below (the fixnum (width field)))
        (incf total (the fixnum (find-out-blockades-count-col field col)))
        (finally (return total))))

(defparameter *cell-neighbours* '(:w :sw :se :e :ne :nw))

(defun members-matches-counter (pos field predicate)
  (declare (optimize (speed 3) (safety 0))
           (type unit-on-map pos)
           (type hextris-map field)
           (type function predicate))
  (iter (declare (declare-variables))
        (with (the fixnum matches-count) = 0)
        (for mem in (members pos))
        (iter (declare (declare-variables))
              (for direction in *cell-neighbours*)
              (for neighbour = (cell-move mem direction))
              (multiple-value-bind (cell filled-p)
                  (map-cell field neighbour)
                (when (funcall predicate neighbour cell filled-p)
                  (incf matches-count))))
        (finally (return matches-count))))

(defun members-touching-something-count (pos field)
  (members-matches-counter
   pos field
   (lambda (neighbour cell filled-p)
     (declare (ignore neighbour))
     (and cell filled-p))))

(defun members-touching-wall-count (pos field)
  (members-matches-counter
   pos field
   (lambda (neighbour cell filled-p)
     (declare (ignore cell filled-p))
     (multiple-value-bind (row col) (cell-row-col neighbour)
       (declare (ignore row))
       (or (< col 0) (>= col (width field)))))))

(defun members-touching-floor-count (pos field)
  (members-matches-counter
   pos field
   (lambda (neighbour cell filled-p)
     (declare (ignore cell filled-p))
     (multiple-value-bind (row col) (cell-row-col neighbour)
       (declare (ignore col))
       (>= row (height field))))))

(defmethod estimate ((solver hedonistic-solver) (field hextris-map) (checking-pos unit-on-map) &key translated-pos)
  (let ((locked-field (unit-lock (unit-on-map-unit checking-pos) (unit-on-map-coord checking-pos) field))
        (translated-pos (or translated-pos (place-on-map (unit-on-map-unit checking-pos) (unit-on-map-coord checking-pos) field))))
    (+ (* (count-rows-fill locked-field) *row-fill-factor*)
       (* (find-out-sum-of-heights locked-field) *sum-of-heights-factor*)
       (* (find-out-burned-rows-count locked-field) *row-burn-factor*)
       (* (find-out-blockades-count locked-field) *blockade-factor*)
       (* (members-touching-something-count translated-pos field) *touching-something-factor*)
       (* (members-touching-wall-count translated-pos field) *touching-wall-factor*)
       (* (members-touching-floor-count translated-pos field) *touching-floor-factor*))))

  
